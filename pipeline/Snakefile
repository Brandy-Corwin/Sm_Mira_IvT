# Change all file paths and change code to be paired end

configfile: "config.yaml"

rule all:
    input:
        "reports/multiqc_report.html"

rule fastq_qc:
    input:
        "../5_fastq/fastq/{accession}.fastq.gz"
    output:
        html="../5_fastq/fastq/qc/{accession}_fastqc.html",
        zip="../5_fastq/fastq/qc/{accession}_fastqc.zip"
    params:
        outdir="../5_fastq/fastq/qc"
    shell:
        "fastqc -t 16 {input} -o {params.outdir}"

rule trim_filter:
    input:
        "../5_fastq/fastq/{accession}.fastq.gz"
    output:
        "../5_fastq/trimmed/{accession}.fastq.gz"
    shell:
        "cutadapt -j 16 -m 20 --cut 10 -o {output} {input}"

rule trim_qc:
    input:
        "../5_fastq/trimmed/{accession}.fastq.gz"
    output:
        html="../5_fastq/trimmed/qc/{accession}_fastqc.html",
        zip="../5_fastq/trimmed/qc/{accession}_fastqc.zip"
    params:
        outdir="../5_fastq/trimmed/qc"
    shell:
        "fastqc -t 16 {input} -o {params.outdir}"

rule fetch_genome:
    output:
        genome="genome/genome.fa",
        annotations="genome/annotations.gtf"
    run:
        shell("wget -nc -O - https://ftp.ebi.ac.uk/pub/databases/wormbase/parasite/releases/WBPS19/species/schistosoma_mansoni/PRJEA36577/schistosoma_mansoni.PRJEA36577.WBPS19.genomic.fa.gz | gzip -f -d > {output.genome}")
        shell("wget -O - https://ftp.ebi.ac.uk/pub/databases/wormbase/parasite/releases/WBPS19/species/schistosoma_mansoni/PRJEA36577/schistosoma_mansoni.PRJEA36577.WBPS19.canonical_geneset.gtf.gz | gzip -f -d > {output.annotations}")

rule star_sa:
    input:
        genome="../2_genome_exploration/genome/genome.fa",
        annotations="../2_genome_exploration/genome/annotations.gtf"
    output:
        directory("../2_genome_exploration/genome/star")
    run:
        shell("STAR \
            --runThreadN 32 \
            --runMode genomeGenerate \
            --genomeDir {output}\
            --genomeFastaFiles {input.genome} \
            --sjdbGTFfile {input.annotations} \
            --sjdbOverhang 289 \
            --genomeSAindexNbases 13 \
            --sjdbGTFfeatureExon CDS")

rule star_align_first:
    input:
        expand("../5_fastq/trimmed/{accession}.fastq.gz", accession=config["ACCESSIONS"]),
        genome_dir="../2_genome_exploration/genome/star",
        manifest="../6_alignment/manifest.tsv"
    output:
        out="../6_alignment/alignment/star/first-pass.final.out",
        out_sj="../6_alignment/alignment/star/SJ.out.tab",
        alignment="../6_alignment/alignment/star/Aligned.sortedByCoord.out.bam",
        outdir=directory("../6_alignment/alignment/star/")
    run:
        shell("STAR \
            --runThreadN 32 \
            --runMode alignReads \
            --genomeDir {input.genome_dir} \
            --readFilesManifest {input.manifest} \
            --readFilesCommand zcat \
            --outSAMtype BAM SortedByCoordinate \
            --outSAMunmapped Within \
            --outFileNamePrefix ../6_alignment/alignment/star/")
        shell("cp ../6_alignment/alignment/star/Log.final.out {output.out}")

rule star_align_second:
    input:
        rules.star_align_first.output.out,
        genome_dir="../2_genome_exploration/genome/star",
        manifest="../6_alignment/manifest.tsv",
        sj="../6_alignment/alignment/star/SJ.out.tab",
        star_dir="../6_alignment/alignment/star/"
    output:
        out="../6_alignment/alignment/star/second-pass.final.out"
    run:
        shell("STAR \
            --runThreadN 32 \
            --runMode alignReads \
            --genomeDir {input.genome_dir} \
            --readFilesManifest {input.manifest} \
            --readFilesCommand zcat \
            --outSAMtype BAM SortedByCoordinate \
            --outSAMunmapped Within \
            --outSAMattributes NH HI AS nM RG \
            --outFileNamePrefix ../6_alignment/alignment/star/ \
            --sjdbFileChrStartEnd {input.sj}")
        shell("cp ../6_alignment/alignment/star/Log.final.out {output.out}")

rule hisat_align:
    input:
        "../2_genome_exploration/genome/genome.fa",
        "../2_genome_exploration/genome/annotations.gtf",
        files="../5_fastq/trimmed/{accession}.fastq.gz",
        index_dir="../2_genome_exploration/genome/hisat/"
    params:
        genome_dir="../2_genome_exploration/genome/hisat/genome"
    output:
        "../6_alignment/alignment/hisat/{accession}.sam"
    run:
        shell("hisat2 {params.genome_dir} -p 32 -U {input.files} --rg-id {wildcards.accession} --rg SM:{wildcards.accession} --summary-file ../6_alignment/alignment/hisat/{wildcards.accession}.log --new-summary > {output}")

rule convert_hisat:
    input:
        "../6_alignment/alignment/hisat/{accession}.sam"
    output:
        "../6_alignment/alignment/hisat/{accession}.bam"
    run:
        shell("samtools view -@ 32 -b {input} | samtools sort -@ 32 > {output}")

rule merge_hisat:
    input:
        expand("../6_alignment/alignment/hisat/{accession}.bam", accession=config["ACCESSIONS"])
    output:
        "../6_alignment/alignment/hisat/merged.bam"
    run:
        shell("samtools merge -@ 32 {output}  {input}")
        shell("samtools index -@ 32 {output}")

rule flagstat:
    input:
        "../6_alignment/alignment/star/second-pass.final.out",
        star="../6_alignment/alignment/star/Aligned.sortedByCoord.out.bam",
        hisat="../6_alignment/alignment/hisat/merged.bam"
    output:
        touch("../7_alignment_qc/flagstat_completed.done")
    run:
        shell("samtools flagstat {input.star}")
        shell("samtools flagstat {input.hisat}")

# Not running right in notebook
# rule qualimap:
#     input:
#         gtf="../2_genome_exploration/genome/annotations.gtf",
#         star="../6_alignment/alignment/star/Aligned.sortedByCoord.out.bam",
#         hisat="../6_alignment/alignment/hisat/merged.bam",
#         check="../7_alignment_qc/flagstat_completed.done"
#     output:
#         no_header_sam="../6_alignment/alignment/star/no_header.sam",
#         no_header_bam="../6_alignment/alignment/star/no_header.bam",
#         outidr_star=directory("../7_alignment_qc/qualimap/star/bam"),
#         outdir=directory("../7_alignment_qc/qualimap/")
#     run:
#         shell("samtools view -h {input.star} | grep -v “^@RG” > {output.no_header_sam}")
#         shell("samtools view -b {output.no_header_sam} > {output.no_header_bam}")
#         shell("qualimap bamqc -nt 32 -outdir {output.outdir_star} -bam {output.no_header_bam} --feature-file {input.gtf} -p strand-specific-forward") 

rule mark_duplicates:
    input:
        "../7_alignment_qc/flagstat_completed.done",
        star="../6_alignment/alignment/star/Aligned.sortedByCoord.out.bam",
        hisat="../6_alignment/alignment/hisat/merged.bam"
    output:
        star="dedup/star.bam",
        hisat="dedup/hisat.bam",
        star_log="logs/star_duplicates",
        hisat_log="logs/hisat_duplicates"
    run:
        shell("picard MarkDuplicates -I {input.star} -M {output.star_log} -O {output.star} --VALIDATION_STRINGENCY SILENT")
        shell("picard MarkDuplicates -I {input.hisat} -M {output.hisat_log} -O {output.hisat} --VALIDATION_STRINGENCY SILENT")

rule count:
    input:
        annotations="../2_genome_exploration/genome/annotations.gtf",
        star="dedup/star.bam",
        hisat="dedup/hisat.bam"
    output:
        star="counts/star_counts.tsv",
        hisat="counts/hisat_counts.tsv"
    run:
        shell("featureCounts -T 32 \
        {input.star} \
        -T 32 \
        --byReadGroup \
        -s 1 \
        --ignoreDup \
        -M \
        --fraction \
        -a {input.annotations} \
        -t 'CDS' \
        -o {output.star} \
        --verbose")
    
        shell("featureCounts -T 32 \
        {input.hisat} \
        -T 32 \
        --byReadGroup \
        -s 1 \
        --ignoreDup \
        -M \
        --fraction \
        -a {input.annotations} \
        -t 'CDS' \
        -o {output.hisat} \
        --verbose")

rule report:
    input:
        expand("../5_fastq/fastq/qc/{accession}_fastqc.html", accession=config["ACCESSIONS"]),
        expand("../5_fastq/trimmed/qc/{accession}_fastqc.html", accession=config["ACCESSIONS"]),
        "../6_alignment/alignment/star/second-pass.final.out",
        expand("../6_alignment/alignment/hisat/{accession}.sam", accession=config["ACCESSIONS"]),
        "../6_alignment/alignment/hisat/merged.bam",
        "../7_alignment_qc/flagstat_completed.done",
        "counts/star_counts.tsv",
        "counts/hisat_counts.tsv"
        #"../6_alignment/alignment/star/no_header.bam"
    params:
        fastq_qc_dir=rules.fastq_qc.params.outdir,
        trim_qc_dir=rules.trim_qc.params.outdir,
        star_align_dir="../6_alignment/alignment/star/",
        hisat_align_dir="../6_alignment/alignment/hisat/",
        counts_dir="counts/"
        #qualimap_dir=rules.qualimap.output.outdir
    output:
        "reports/multiqc_report.html"
    shell:
        "multiqc --force -d {params.fastq_qc_dir} {params.trim_qc_dir} {params.star_align_dir} {params.hisat_align_dir} {params.counts_dir} -n {output}"
        # ADD {params.qualimap_dir} to output